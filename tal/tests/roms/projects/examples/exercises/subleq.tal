( Subleq: The subleq instruction subtracts the contents at address a from the contents at address b, stores the result at address b, and then, if the result is not positive, jumps to address c. If the result is positive, execution proceeds to the next instruction in sequence. )

|0100

@on-reset ( -> )
	#0000
	&while ( -- )
		eval DUP2 #8000 LTH2 ?&while
	POP2
	( halt ) #010f DEO
	BRK

@eval ( ip* -- ip* )
	DUP2 get ,&a STR2
	INC2 DUP2 get ,&b STR2
	INC2 DUP2 get ,&c STR2
	INC2
	( | I/O )
	[ LIT2 &a $2 ] #ffff EQU2 ?&input
	[ LIT2 &b $2 ] #ffff EQU2 ?&output
	( | SUBLEQ )
	( sub ) ,&b LDR2 get ,&a LDR2 get SUB2 DUP2
	( write ) ,&b LDR2 DUP2 ADD2 ;program ADD2 STA2
	( leq ) #0001 SUB2 #8000 LTH2 ?&end
	POP2 [ LIT2 &c $2 ] &end JMP2r
	&input ( -- )
		( TODO ) JMP2r
	&output ( -- )
		,&a LDR2 get NIP #18 DEO
		JMP2r

@get ( a* -- b* )
	DUP2 ADD2 ;program ADD2 LDA2 JMP2r

@program ( hello world )
	[
	000f 0011 ffff 0011 ffff ffff 0010 0001
	ffff 0010 0003 ffff 000f 000f 0000 0000
	ffff 0048 0065 006c 006c 006f 002c 0020
	0077 006f 0072 006c 0064 0021 000a 0000 ]

